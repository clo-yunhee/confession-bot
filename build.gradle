import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.4.32'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

group 'fr.cloyunhee.confessionbot'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    shadow localGroovy()
    shadow gradleApi()

    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    implementation 'com.discord4j:discord4j-core:3.1.5'
    implementation 'ch.qos.logback:logback-classic:1.2.3'
    implementation 'org.jetbrains.exposed:exposed-core:0.32.1'
    implementation 'org.jetbrains.exposed:exposed-dao:0.32.1'
    implementation 'org.jetbrains.exposed:exposed-jdbc:0.32.1'
    implementation 'org.postgresql:postgresql:42.2.21'
}

compileKotlin {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    kotlinOptions {
        jvmTarget = "1.8"
        apiVersion = "1.4"
        languageVersion = "1.4"
    }
}

jar {
    manifest {
        attributes 'Main-Class': 'fr.cloyunhee.confessionbot.Main'
    }
}

task relocateShadowJar(type: ConfigureShadowRelocation) {
    target = tasks.shadowJar
}

tasks.shadowJar.dependsOn tasks.relocateShadowJar

task stage(dependsOn: ['build', 'clean', 'shadowJar'])
build.mustRunAfter clean

gradle.taskGraph.whenReady {
    taskGraph ->
        if (taskGraph.hasTask(stage)) {
            test.enabled = false
        }
}